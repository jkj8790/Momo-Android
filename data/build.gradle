
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion 27
  buildToolsVersion '27.0.3'


  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 27
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  packagingOptions {
    exclude 'META-INF/rxjava.properties'
  }

  flavorDimensions "data_source"

  productFlavors {
    fake {
      dimension "data_source"
    }

    disk {
      dimension "data_source"
    }
  }

  // because kotlin now has "first class" support in android
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation 'io.reactivex.rxjava2:rxkotlin:2.0.3'
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

  implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
  implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
  implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
  implementation 'com.squareup.moshi:moshi:1.5.0'
  implementation 'javax.inject:javax.inject:1'
  implementation 'com.squareup.sqlbrite3:sqlbrite-kotlin:3.1.0'
  implementation 'android.arch.persistence:db-framework:1.0.0'
  implementation "org.jetbrains.anko:anko-sqlite:$anko_version"


  implementation "com.android.support:appcompat-v7:$supportLibraryVersion"

  implementation 'com.google.dagger:dagger:2.13'
  kapt 'com.google.dagger:dagger-compiler:2.13'


  testImplementation 'junit:junit:4.12'
  testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
  testImplementation "com.natpryce:hamkrest:1.4.0.0"

  androidTestCompile 'com.android.support.test:rules:1.0.2'
  androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  api project(path: ':domain')
}

task copyTestClasses(type: Copy) {
  from "build/tmp/kotlin-classes/debugUnitTest"
  into "build/intermediates/classes/debug"
}

task ktlint(type: JavaExec, group: "verification") {
  description = "Check Kotlin code style."
  classpath = configurations.ktlint
  main = "com.github.shyiko.ktlint.Main"
  args "--android", "src/**/*.kt"
  // to generate report in checkstyle format prepend following args:
  // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
  // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
  description = "Fix Kotlin code style deviations."
  classpath = configurations.ktlint
  main = "com.github.shyiko.ktlint.Main"
  args "-F", "--android", "src/**/*.kt"
}